
BufferedInStream struct : Readable{
    
    private handle Readable*;
    
    private buffer      char*;
    // Maximum number of bytes the buffer can store. The amount
    // of bytes actuall read from the handle may be less than this.
    //
    // buffer_length reflects the amount actually read from handle.
    private buffer_size int;
    
    private buffer_length := 0;
    // Position offset into the buffer.
    private pos           := 0;
    
    BufferedInStream(handle Readable*, buffer_size := 8192)
        : handle(handle) {
        if buffer_size < 0 {
            panic("buffer_size < 0");
        }
        /*if handle == FlushHandler {
            (cast(FlushHandler*) handle).set_flushable(handle);
        }*/
        this.buffer_size = buffer_size;
        buffer = new char[buffer_size];
    }
    
    BufferedInStream moveobj(o BufferedInStream*)
        : handle(o.handle),
          buffer(o.buffer),
          buffer_size(o.buffer_size),
          buffer_length(o.buffer_length),
          pos(o.pos) {
        o.handle = null;
        o.buffer = null;
        o.buffer_size = 0;
        o.buffer_length = 0;
        o.pos = 0;
    }

    /*
     * This should only ever be called if the handle is on
     * the stack and the handle now has new memory and needs
     * redirection.
     */
    fn reapply_handle(handle Readable*) {
        /*if handle == FlushHandler {
            (cast(FlushHandler*) handle).set_flushable(handle);
        }*/
        this.handle = handle;
    }

    /*
     * When at the end of the current buffer_length read the next bytes
     * into the buffer. Will try and read buffer_size but may read less.
     */
    private fn fill() {
        // TODO: should make sure the stream is still open.
        if pos >= buffer_length {
            buffer_length = handle.read_buffer(buffer, buffer_size);
        }
    }
    
    /*
     * Reads a single character from the stream. If the end
     * of stream is reached this function returns -1 instead.
     */
    fn read() int {
        fill();
        if pos >= buffer_length {
            return -1; // End of stream.
        }
    
        return buffer[pos++];
    }
    
    /*
     * Tries to read length bytes into a the pre-allocated buffer
     * and returns the amount of bytes read.
     */
    fn read_buffer(buffer char*, length int) int {
        available := buffer_length - pos;
        if available >= length {
            // TODO: ensure stream is still open?
            // There is already enough bytes read from the handle
            // to be returned to the user.
            memcpy(buffer, this.buffer + pos, length);
            pos += length;
            return length;
        }
        
        amount_read := 0;
        if available > 0 {
            memcpy(buffer, this.buffer + pos, available);
            amount_read += available;
            pos = available;
        }
        
        // TODO: This should actually round page to the buffer_length and
        // read that amount from the handle and put the rest in this buffer.

        // No reason to continually fill our buffer just have
        // the handle do the work of reading the remaining bytes.
        amount_read += handle.read_buffer(buffer + amount_read, length - amount_read);
        
        return amount_read;
    }
    
    ~BufferedInStream() {
        delete buffer;
    }
}